import org::eclipse::emf::ecore;
import org::applause::lang::applauseDsl;

extension templates::MetamodelExtensions;

className(View this):
	this.name.toFirstUpper() + "ViewController";

filenameHeader(View this):
	className() + ".h";
	
filenameModule(View this):
	className() + ".m";
	
centralProvidersClassName(Application this):
	this.name + "Providers";
	
filenameCentralProvidersHeader(Application this):
	centralProvidersClassName() + ".h";
	
filenameCentralProvidersModule(Application this):
	centralProvidersClassName() + ".m";
	
applicationDelegateClassname(Application this):
	"DKC" + this.name + (this.name.endsWith('App') ? "" : "App") + "Delegate";
	
tableCellClassname(SectionCell this):
	this.type == CellType::MAPS
		? "IPTableViewCellMap"
		: "UITableViewCell";
		
filenameApplicationDelegateHeader(Application this):
	applicationDelegateClassname() + '.h';

filenameApplicationDelegateModule(Application this):
	applicationDelegateClassname() + '.m';


neededImports(WebView this):
	{};

neededImports(View this):
	{"IPContentProvider.h"};
	
neededImports(SectionedView this):
	sections.cells.action.reject(e|e==null).neededImports().flatten().toSet();
	
neededImports(ViewAction this):
	{};	
	
neededImports(ViewCall this):
	provider.neededImports().union({view.filenameHeader()});
	
neededImports(ProviderConstruction this):
	{"IPContentProvider.h"};
	
neededImports(ComplexProviderConstruction this):
	{application().filenameCentralProvidersHeader()};

baseClassName(View this):
	"";
	
baseClassName(WebView this):
	"UIViewController";	
	
baseClassName(TableView this):
	"IPTableViewController";

baseClassName(DetailsView this):
	"IPDetailsViewController";
	
baseClassFilenameHeader(View this):
	baseClassName() + ".h";

type(SectionCell this):
	type.toString().toFirstUpper();
	
firstCell(TableView this):
	sections.first().cells.first();
	
expression(Expression s, String kvcTarget, String kvcPrefix):
	"<!ERROR!" + s.metaType + ">";
	
expression(CollectionExpression s, String kvcTarget, String kvcPrefix):
	"<!ERROR!" + s.metaType + ">";
	
expression(StringSplit s, String kvcTarget, String kvcPrefix):
	"[" + expression(s.value, kvcTarget, kvcPrefix) + " componentsSeparatedByString: " + expression(s.delimiter, kvcTarget, kvcPrefix) + "]";

expression(ScalarExpression s, String kvcTarget, String kvcPrefix):
	"<!ERROR!" + s.metaType + ">";
	
expression(StringLiteral this, String kvcTarget, String kvcPrefix):
	'@"' + value + '"';
	
expression(StringConcat this, String kvcTarget, String kvcPrefix):
	'[NSString stringWithFormat:@"' 
		+ values.collect(e|"%@").toString("") + '", '
		+ values.expression(kvcTarget, kvcPrefix).toString(", ") + "]";
		
expression(StringUrlConform this, String kvcTarget, String kvcPrefix):
	'[' + value.expression(kvcTarget, kvcPrefix) + ' stringByAddingPercentEscapesUsingEncoding:NSUTF8StringEncoding]';
	
expression(StringReplace this, String kvcTarget, String kvcPrefix):
	'[' + 
	value.expression(kvcTarget, kvcPrefix) +
	' stringByReplacingOccurrencesOfString:' + 
	match.expression(kvcTarget, kvcPrefix) +
	' withString:' +
	replacement.expression(kvcTarget, kvcPrefix) +
	']';
	
List[String] keyPath(ObjectReference this):
	tail == null ? {object.name} : {object.name}.addAll(tail.keyPath());

List[String] keyPath(ObjectReference this, String kvcPrefix):
	let head = (kvcPrefix != "" ? {kvcPrefix} : {}):
		head.addAll(keyPath().withoutFirst());
	
expression(VariableDeclaration this, String kvcTarget, String kvcPrefix):
	"!" + this.metaType;
	
expression(Constant this, String kvcTarget, String kvcPrefix):
	value.get(0).expression(kvcTarget, kvcPrefix).replaceAll("ü","ue").replaceAll("ß","ss"); 	
	
expression(ObjectReference this, String kvcTarget, String kvcPrefix):
	Constant.isInstance(this.object)
		? this.object.expression(kvcTarget, kvcPrefix)
		: (let keyPath = keyPath(kvcPrefix):
				keyPath.isEmpty ? 
				kvcTarget : 
				'[' + kvcTarget + ' safeValueForKeyPath:@"' + keyPath.toString('.') + '"]'
		);
	
contentProvider(ProviderConstruction this, String providers, String kvcTarget, String kvcPrefix):
	"<!ERROR!" + metaType + ">";

contentProvider(SimpleProviderConstruction this, String providers, String kvcTarget, String kvcPrefix):
	'[IPSimpleContentProvider providerWithContent:' +
	expression.expression(kvcTarget, kvcPrefix) +
	 ' andProviders:self.contentProvider.providers]';
	 
contentProvider(ComplexProviderConstruction this, String providers, String kvcTarget, String kvcPrefix):
	'[' + providers + ' providerFor' + provider.name + 
		(argument == null ? '' : ': ' + argument.expression(kvcTarget, kvcPrefix) )
	+ ']';
	
queryImage(SectionCell this, String kvcTarget):
	this.type == CellType::MAPS
		? kvcTarget + ".imageUrl"
		: null;	
	
rowCount(SectionCell this):
	iterator == null ? 1 : '[[' + iterator.collection.expression('contentProvider', 'content') +' asArray] count]';

anySpecificRowHeight(SectionedView this):
	sections.anySpecificRowHeight().exists(e|e);
	
private anySpecificRowHeight(ViewSection this):
	cells.specificRowHeight().exists(e|e>0);

specificRowHeight(SectionCell this):
	this.type == CellType::MAPS
		? 88
		: 0;
	
methodSignature(ContentProvider p):
	"-(IPContentProvider*)providerFor" + p.name + (
		p.parameter != null ? ':(id)' + p.parameter.name : '') 
	;
	
providersWithImplementation(Application this):
	model().elements.typeSelect(ContentProvider).select(e|e.xml);
	
sections(SectionedView this):
	sections.size + superView.sections.size;	

forAllSections(SectionedView this): 
	forAllSections == null ? superView.forAllSections : forAllSections;
		
	
 selection(ProviderConstruction this):
	ComplexProviderConstruction.isInstance(this)
		? ((StringLiteral)((ComplexProviderConstruction)this).provider.selection).value
		: ((StringLiteral)((SimpleProviderConstruction)this).expression).value;
		
 parameter(ProviderConstruction this):
	ComplexProviderConstruction.isInstance(this)
		? (SectionId.isInstance(((ComplexProviderConstruction)this).predefinedParameter) ? "section+1": ((ComplexProviderConstruction)this).predefinedParameter) 				
		: "no parameter for SimpleProviderConstruction!";		
		
		
		
getUrl(WebView this):
	allViewCalls().select(call|call.view == this).first().provider.contentProvider().url.value();